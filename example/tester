#!/usr/local/bin/coffee
# test formula against csv

fs = require 'fs'
csv = require 'csv'
parser = require '../dst/parser.js'
evaluator = require '../src/evaluator.coffee'

main = (argv) ->
  if !argv[1]? or argv[3]?
    console.log "Usage: #{argv[0]} FORMULA_FILE [ CSV_FILE ]"
    console.log "       If you do not provide a CSV file, a stub will be created"
    console.log "         by analyzing the formula."
    console.log "       If you provide a CSV file in the right format, the tests"
    console.log "         contained will be run against the formula."
    return
  if !argv[2]?
    if fs.existsSync argv[1]
      formula = parser.parse fs.readFileSync(argv[1]).toString()
      tree = evaluator.build formula
      lets = tree.unbound()
      lets.push 'expected'
      lets.push 'message'
      console.log lets.join ','
    else
      console.error "Formula file #{argv[1]} not found"
    return
  if fs.existsSync argv[2]
    formula = parser.parse fs.readFileSync(argv[1]).toString()
    csv().from(fs.readFileSync(argv[2]).toString(), columns: true).transform((data) ->
      data.actual = evaluator.evaluate formula, data
      data
    ).on 'record', (data, index) ->
      message = "Row #{index+1}:  expected #{data.actual} to equal #{data.expected}.  #{data.message}."
      console.log message if data.actual isnt data.expected
  else
    console.error "Data file #{argv[2]} not found"

main process.argv.slice 1
